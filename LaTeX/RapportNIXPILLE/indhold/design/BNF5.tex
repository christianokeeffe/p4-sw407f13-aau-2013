\subsection{The BNF of SPLAD}
This section contains the BNF for the programming language of this project; SPLAD. It is clear that the BNF begins with the non-terminal "program". The "program" non-terminal can then be derived in a number of ways, to represent a specific program. The grammar for SPLAD is:

\begin{grammar}
<program> $\ra$ <roots>

<roots> $\ra$ $\eps$
\alt <root> <roots>
 
<root> $\ra$ <dcl>;
\alt <callid> <assign>;
\alt <function>
\alt <COMMENT>

<dcl> $\ra$ <type> <id> <dclend>

<type> $\ra$ <primitivetype> <arraytype>

<primitivetype> $\ra$ bool
\alt double
\alt int
\alt char
\alt container
\alt string

<arraytype> $\ra$ [ ]
\alt$\eps$

<id> $\ra$ <LETTER>

<dclend> $\ra$ $\eps$
\alt <assign> 

<assign> $\ra$ "<""-""-" <expr>

<expr> $\ra$ <term> <exprend>

<term> $\ra$ <comp> <termend>

<comp> $\ra$ <factor> <compend>

<factor> $\ra$ ( <expr> )
	\alt !(<expr>)
	\alt <callid>
	\alt <numeric>
	\alt <string>
	\alt <functioncall> 
	\alt <cast>
	\alt LOW
	\alt HIGH
	\alt true
	\alt false

<callid> $\ra$ <id> <arrayiden>

<arrayiden> $\ra$ <ARRAYCALL>
\alt $\eps$

<ARRAYCALL> $\ra$ [<NOTZERODIGIT>]

<numeric> $\ra$ <plusminus> <DIGIT> <numericend>

<plusminus> $\ra$ $\eps$
	\alt -
	\alt +

<numericend> $\ra$ $\eps$
\alt . <DIGIT>

<string> $\ra$ <STRINGTOKEN>

<functioncall> $\ra$ call <id> (<callexpr>)

<callexpr> $\ra$ <subcallexpr>
\alt$\eps$

<subcallexpr> $\ra$ <expr> <subcallexprend>

<subcallexprend> $\ra$ , <subcallexpr>
\alt$\eps$

<cast> $\ra$ <type> (<expr>)

<compend> $\ra$ <comparisonoperator> <comp>
\alt$\eps$

<comparisonoperator> $\ra$ ">"
				\alt "<"
				\alt "<="
				\alt ">="
				\alt "!="
				\alt "="

<termend> $\ra$ <termsymbol> <term>
\alt$\eps$

<termsymbol> $\ra$ *
\alt /
\alt AND 

<exprend> $\ra$ <exprsymbol> <expr>
\alt$\eps$

<exprsymbol> $\ra$ +
\alt -
\alt OR 

<function> $\ra$ function <id> return <functionmidt>

<functionmidt> $\ra$ <type> <functionend> <expr>; end
\alt nothing <functionend> nothing; end

<functionend> $\ra$
using (<params>)
begin
	<stmts>
	return

<params> $\ra$ <subparams>
	\alt$\eps$

<subparams> $\ra$ <type> <id> <subparamsend>

<subparamsend> $\ra$ , <subparams>
\alt$\eps$

<stmts> $\ra$ $\eps$
	\alt <stmt> <stmts>

<stmt> $\ra$ <callid> <assign>;
	\alt <nontermif>
	\alt <nontermwhile>
	\alt <from>
	\alt <dcl>;
	\alt <functioncall>;
	\alt <nontermswitch>
	\alt <COMMENT>
	
<nontermif> $\ra$ if(<expr>)
	begin
		<stmts>
	end
	<endif>

<endif> $\ra$ 
	else <nontermelse>
	\alt$\eps$

<nontermelse> $\ra$ <nontermif>
	\alt begin
		<stmts>
	end

<nontermwhile> $\ra$ while(<expr>)
		begin
			<stmts>
		end
		
<from> $\ra$ from <expr> to <expr> step <callid> <assign>
	begin
		<stmts>
	end

<nontermswitch> $\ra$ switch (<expr>)
		begin
			<cases>
		end

<cases> $\ra$ case <expr>:
			<stmts>
		<endcase>
		
<endcase> $\ra$ <cases>
		\alt break; <breakend>
		\alt default:
			<stmts>
			break;

<breakend> $\ra$ <cases>
\alt default:
<stmts>
break;
\alt$\eps$

<STRINGTOKEN> $\ra$ '' .*? '' 

<LETTER> $\ra$ [a - zA - Z]$~^+$

<DIGIT> $\ra$ [0 - 9]$~^+$

<NOTZERODIGIT> $\ra$ [1-9][0-9]$~^*$

<COMMENT> $\ra$ /* .*? */


\end{grammar}